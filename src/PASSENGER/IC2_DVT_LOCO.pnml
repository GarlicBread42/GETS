//###################################
//###DB "IC2" Bi-Level DVT as Loco###
//###################################			
			
//Spriteset 1. Klasse

spriteset (spriteset_IC2_DVT_LOCO, "gfx\IC2\IC2_DVT_LOCO_1x_8bpp.png")
					{template_158trains_1x(0, 0)}	
		alternative_sprites (spriteset_IC2_DVT_LOCO, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "gfx\IC2\IC2_DVT_LOCO_1x_32bpp.png", "gfx\IC2\IC2_DVT_LOCO_1x_mask.png")
					{template_158trains_1x(0,0)}
		alternative_sprites (spriteset_IC2_DVT_LOCO, ZOOM_LEVEL_IN_2X, BIT_DEPTH_32BPP, "gfx\IC2\IC2_DVT_LOCO_2x_32bpp.png", "gfx\IC2\IC2_DVT_LOCO_2x_mask.png")
					{template_158trains_2x(0,0)}				
		alternative_sprites (spriteset_IC2_DVT_LOCO, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP, "gfx\IC2\IC2_DVT_LOCO_4x_32bpp.png", "gfx\IC2\IC2_DVT_LOCO_4x_mask.png")
					{template_158trains_4x(0,0)}	
					
spriteset (spriteset_IC2_DVT_LOCO_loading, "gfx\IC2\IC2_DVT_LOCO_loading_1x_8bpp.png")
					{template_158trains_1x(0, 0)}	
		alternative_sprites (spriteset_IC2_DVT_LOCO_loading, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "gfx\IC2\IC2_DVT_LOCO_loading_1x_32bpp.png", "gfx\IC2\IC2_DVT_LOCO_loading_1x_mask.png")
					{template_158trains_1x(0,0)}
		alternative_sprites (spriteset_IC2_DVT_LOCO_loading, ZOOM_LEVEL_IN_2X, BIT_DEPTH_32BPP, "gfx\IC2\IC2_DVT_LOCO_loading_2x_32bpp.png", "gfx\IC2\IC2_DVT_LOCO_loading_2x_mask.png")
					{template_158trains_2x(0,0)}				
		alternative_sprites (spriteset_IC2_DVT_LOCO_loading, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP, "gfx\IC2\IC2_DVT_LOCO_loading_4x_32bpp.png", "gfx\IC2\IC2_DVT_LOCO_loading_4x_mask.png")
					{template_158trains_4x(0,0)}						

spritegroup spritegroup_IC2_DVT_LOCO_loading
		{loaded: 	[spriteset_empty];
			loading: 	[spriteset_IC2_DVT_LOCO_loading];}	

//Spritesets purchase

spriteset (spriteset_IC2_DVT_LOCO_purchase, "gfx\IC2\IC2_DVT_LOCO_1x_8bpp.png")
					{template_purchase(0, 0)}	
		alternative_sprites (spriteset_IC2_DVT_LOCO_purchase, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "gfx\IC2\IC2_DVT_LOCO_1x_32bpp.png", "gfx\IC2\IC2_DVT_LOCO_1x_mask.png")
					{template_purchase(0,0)}
															
//Switches
							
						switch(FEAT_TRAINS, SELF, switch_spritestack_IC2_DVT_LOCO, [STORE_TEMP((getbits(extra_callback_info1, 8, 8) < 1 ? CB_FLAG_MORE_SPRITES : 0) | PALETTE_USE_DEFAULT, 0x100), getbits(extra_callback_info1, 8, 8)]) {
							0: spriteset_IC2_DVT_LOCO;
							1: spritegroup_IC2_DVT_LOCO_loading;}															
					
						switch(FEAT_TRAINS, SELF, switch_length_IC2_DVT_LOCO, position_in_articulated_veh % 3) {
							0: 4;
							1: 7;
							2: 4;}

						switch(FEAT_TRAINS,PARENT,switch_running_cost_IC2_DVT_LOCO, 1){
							return 3750;}	
													
						switch(FEAT_TRAINS, SELF, switch_IC2_DVT_LOCO_graphics, position_in_articulated_veh  % 3) {
							0:     spriteset_empty;
							1:     return switch_spritestack_IC2_DVT_LOCO;
							2:     spriteset_empty;}
							
						switch (FEAT_TRAINS, SELF, switch_articulated_IC2_DVT_LOCO, extra_callback_info1) {
							1 .. 2: return ITEM_IC2_DVT_LOCO;	
								return CB_RESULT_NO_MORE_ARTICULATED_PARTS;}

item (FEAT_TRAINS , ITEM_IC2_DVT_LOCO, 5080)
		{
	//property Block
			property 
			{
			//common properties
						name: 							string(STR_NAME_IC2_DVT_LOCO);
						extra_flags:					bitmask(VEHICLE_FLAG_SYNC_VARIANT_EXCLUSIVE_PREVIEW);
						climates_available: 			ALL_CLIMATES;
						introduction_date: 				date(2015,01,01);
						model_life: 					VEHICLE_NEVER_EXPIRES;
						vehicle_life: 					40;
						retire_early:					-10;						
						reliability_decay: 				20;
						misc_flags: 					bitmask(TRAIN_FLAG_SPRITE_STACK);	
						cargo_allow_refit:              [PASS, TOUR];
						loading_speed:					6;
						cargo_capacity:                 31 ;
						cost_factor: 					108;
						running_cost_factor: 			90;
						running_cost_base:              RUNNING_COST_DIESEL;	
						
			//Train specific properties
						sprite_id: 						SPRITE_ID_NEW_TRAIN;
						speed: 							160 km/h;
						refit_cost: 					0;
						track_type: 					RAIL;	
						power:  						1 hp;
						weight: 						58 ton;
						ai_engine_rank: 				0;
						engine_class: 					ENGINE_CLASS_DIESEL;
						visual_effect_and_powered: 		visual_effect_and_powered(VISUAL_EFFECT_DISABLE, 2, DISABLE_WAGON_POWER);
						dual_headed: 					0;
						tractive_effort_coefficient: 	0.001;
						length: 						8;
											
			}
			
	//Vehicels Graphics
			graphics
			{
			default:						switch_IC2_DVT_LOCO_graphics;
			additional_text:				string(STR_DESC_IC2_DVT_LOCO);
			length:							switch_length_IC2_DVT_LOCO;
			articulated_part:				switch_articulated_IC2_DVT_LOCO;
			purchase:						spriteset_IC2_DVT_LOCO_purchase;
			cost_factor:					31;
			running_cost_factor:			switch_running_cost_IC2_DVT_LOCO;					
			 }
		}		
